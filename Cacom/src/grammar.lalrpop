//
// The lexer and some of the grammar rules were inspired
// by Konrad Siek FML Language intepreter, thanks Konrad!
// https://github.com/kondziu
//

use crate::ast::{AST, Opcode, IntoBoxed};
use crate::grammar::AST::NoneVal;
use std::collections::VecDeque;
use std::str::FromStr;

grammar;

// Lexer
match {
    // Comments, those we ignore
    r"\s*" => { },
    r"/\*([^*]|[\r\n]|(\*+([^*/]|[\r\n])))*\*+/|(//.*)" => { },

    // keywords
    ";" => SEMICOLON,
    "{" => CURLYBOPEN,
    "}" => CURLYBCLOSE,
    "||" => OR,
    "&&" => AND,
    "!=" => NEQ,
    "==" => EQ,
    "="  => ASSIGN,
    ">"  => GREATER,
    "<"  => LESS,
    ">=" => GREATEREQ,
    "<=" => LESSEQ,
    "*"  => MULTIPLY,
    "/"  => DIVIDE,
    "%"  => MODULE,
    "+"  => PLUS,
    "-" => MINUS,
    "if" => IF,
    "else" => ELSE,
    "(" => LPAREN,
    ")" => RPAREN,
    "let" => LET,
    "in" => IN,
    "print" => PRINT,
    "[" => RBRACKET,
    "]" => LBRACKET,
    "true" => TRUE,
    "false" => FALSE,
    "def" => DEF,
    "," => COMMA,
    "return" => RETURN,

    r"[_A-Za-z][_A-Za-z0-9]*" => IDENTIFIER,
    r"[-]?[0-9]+" => NUMBER,
    // TODO FLOATS
    r#""([^\\"]|\\[~ntr\\"])*""# => STRING_LITERAL,
}

pub TopLevel: AST = {
    <toplevel: TopLevelExpressions> => AST::Top(toplevel.into_boxed()),
                                    => AST::Top(vec![NoneVal.into_boxed()]),
}

TopLevelExpressions: Vec<AST> = {
    <first: TopLevelExpression> <others: (SEMICOLON <TopLevelExpression>)*> SEMICOLON? => {
        let mut all = VecDeque::from(others);
        all.push_front(first);
        Vec::from(all)
    }
}

TopLevelExpression: AST = {
    AExpr => <>,
    FunDecl => <>,
}

Expressions: Vec<AST> = {
    <first: Expression> <others: (SEMICOLON <Expression>)*> SEMICOLON? => {
        let mut all = VecDeque::from(others);
        all.push_front(first);
        Vec::from(all)
    }
}

// Those are 'top' level expression in block,
// do not put things that can appear in operations
// (ie. in binary operations, conditions and so on...)
// here belongs for example while or for cycle.
Expression: AST = {
    AExpr => <>, // All subexpressions - contains binaryop, ifs, calls and so on.
    Return => <>,
}

AExpr: AST = {
    <left:AExpr> <op:ExprOp> <right:Factor> => AST::Operator{op: op, arguments: vec![Box::new(left), Box::new(right)]},
    Factor,
}

Acceptance: AST = {
    Number => <>,
    LPAREN <expr: AExpr> RPAREN => expr,
    Block => <>,
    Conditional => <>
}

Factor: AST = {
    <left:Factor> <op:TermOp> <right:Term> => AST::Operator{op: op, arguments: vec![Box::new(left), Box::new(right)]},
    Term,
}

ExprOp: Opcode = {
    PLUS => Opcode::Add,
    MINUS => Opcode::Sub,
}

TermOp: Opcode = {
    MULTIPLY => Opcode::Mul,
    DIVIDE => Opcode::Div,
}

Term: AST = {
    // TODO: floats
    Acceptance => <>,
}

Number: AST = {
    NUMBER => AST::Integer(i32::from_str(<>).unwrap()),
}

Block: AST = {
    CURLYBOPEN <expressions: Expressions> CURLYBCLOSE => AST::Block(expressions.into_boxed()),
    CURLYBOPEN CURLYBCLOSE => AST::NoneVal,
}

FunDecl: AST = {
    DEF <name: Identifier> <parameters: Parameters> ASSIGN <body: Expression> => {
        AST::Function{name, parameters, body: body.into_boxed()}
    }
}

Identifier: String = IDENTIFIER => <>.to_string();

Parameters: Vec<String> = {
    LPAREN <elements: (<Identifier> COMMA)*> <element: Identifier?> RPAREN =>
        match element {
            None => elements,
            Some(e) => { let mut elements = elements; elements.push(e); elements }
        }
}

Conditional: AST = {
    IF <guard: AExpr> <then: Block> ELSE <els: Block> =>
                    AST::Conditional{guard: guard.into_boxed(),
                            then_branch: then.into_boxed(),
                            else_branch: Some(els.into_boxed())},

    IF <guard: AExpr> <then: Block> =>
                    AST::Conditional{guard: guard.into_boxed(),
                            then_branch: then.into_boxed(),
                            else_branch: None}
}

Return: AST = {
    RETURN <expr: AExpr> => AST::Return(expr.into_boxed())
}
