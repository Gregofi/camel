// The lexer and some of the grammar rules were inspired
// by Konrad Siek FML Language intepreter, thanks Konrad!
// https://github.com/kondziu

use crate::ast::{AST, Opcode};
use std::collections::VecDeque;
use std::str::FromStr;

grammar;

// Lexer
match {
    // Comments, those we ignore
    r"\s*" => { },
    r"/\*([^*]|[\r\n]|(\*+([^*/]|[\r\n])))*\*+/|(//.*)" => { },

    // keywords
    ";" => SEMICOLON,
    "{" => CURLYBOPEN,
    "}" => CURLYBCLOSE,
    "||" => OR,
    "&&" => AND,
    "!=" => NEQ,
    "==" => EQ,
    "="  => ASSIGN,
    ">"  => GREATER,
    "<"  => LESS,
    ">=" => GREATEREQ,
    "<=" => LESSEQ,
    "*"  => MULTIPLY,
    "/"  => DIVIDE,
    "%"  => MODULE,
    "+"  => PLUS,
    "-" => MINUS,
    "if" => IF,
    "else" => ELSE,
    "(" => LPAREN,
    ")" => RPAREN,
    "let" => LET,
    "in" => IN,
    "[" => RBRACKET,
    "]" => LBRACKET,
    "true" => TRUE,
    "false" => FALSE,
    "def" => DEF,
    "," => COMMA,
    "return" => RETURN,

    r"[_A-Za-z][_A-Za-z0-9]*" => IDENTIFIER,
    r"[-]?[0-9]+" => NUMBER,
    // TODO FLOATS
    r#""([^\\"]|\\[~ntr\\"])*""# => STRING_LITERAL,
}

pub TopLevel: AST = {
    <toplevel: TopLevelExpressions> => AST::Top(toplevel),
                                    => AST::Top(vec![AST::NoneVal]),
}

TopLevelExpressions: Vec<AST> = {
    <mut elements: (<TopLevelExpression> SEMICOLON)*> <element: TopLevelExpression> SEMICOLON? => {
        elements.push(element);
        elements
    }
}

TopLevelExpression: AST = {
    // FIXME: This permits 'return' in top level
    Statement => <>,
    FunDecl => <>,
}

Statements: Vec<AST> = {
    <mut elements: (<Statement> SEMICOLON)*> <element: Statement> SEMICOLON? => {
        elements.push(element);
        elements
    }
}

// Those are 'top' level expression in block,
// do not put things that can appear in operations
// (ie. in binary operations, conditions and so on...)
// here belongs for example while or for cycle.
Statement: AST = {
    Expr => <>, // All subexpressions - contains binaryop, ifs, calls and so on.
    Return => <>,
}

Expr: AST = {
    <left:Expr> <op:LogicalOp> <right: AExpr> => AST::Operator{op: op, arguments: vec![left, right]},
    AExpr,
}

AExpr: AST = {
    <left:AExpr> <op:ExprOp> <right:Factor> => AST::Operator{op: op, arguments: vec![left, right]},
    Factor,
}

Factor: AST = {
    <left:Factor> <op:TermOp> <right:Term> => AST::Operator{op: op, arguments: vec![left, right]},
    Term,
}

Term: AST = {
    // TODO: floats
    LowPrio => <>,
}

LowPrio: AST = {
    TRUE => AST::Bool(true),
    FALSE => AST::Bool(false),
    Number => <>,
    String => AST::String(<>),
    Call => <>,
    LPAREN <expr: Expr> RPAREN => expr,
    Block => <>,
    Conditional => <>,
    Identifier => AST::AccessVariable{name: <>},
}

Call: AST = {
    <function: Identifier> LPAREN <arguments: Arguments> RPAREN => {
        AST::CallFunction{ name: function, arguments: arguments }
    }
    // TODO: Method calls over objects, lists, strings and so on.
}

LogicalOp: Opcode = {
    LESS => Opcode::Less,
    LESSEQ => Opcode::LessEq,
    GREATER => Opcode::Greater,
    GREATEREQ => Opcode::GreaterEq,
    EQ => Opcode::Eq,
}

ExprOp: Opcode = {
    PLUS => Opcode::Add,
    MINUS => Opcode::Sub,
}

TermOp: Opcode = {
    MULTIPLY => Opcode::Mul,
    DIVIDE => Opcode::Div,
}

Number: AST = {
    NUMBER => AST::Integer(i32::from_str(<>).unwrap()),
}

String: String = {
    <s:STRING_LITERAL> => s[1..s.len()-1].to_string(),
}

Block: AST = {
    CURLYBOPEN <expressions: Statements> CURLYBCLOSE => AST::Block(expressions),
    CURLYBOPEN CURLYBCLOSE => AST::NoneVal,
}

FunDecl: AST = {
    DEF <name: Identifier> <parameters: Parameters> ASSIGN <body: Statement> => {
        AST::Function{name, parameters, body: Box::new(body)}
    }
}

Identifier: String = IDENTIFIER => <>.to_string();

Parameters: Vec<String> = {
    LPAREN <elements: (<Identifier> COMMA)*> <element: Identifier?> RPAREN =>
        match element {
            None => elements,
            Some(e) => { let mut elements = elements; elements.push(e); elements }
        }
}

Arguments: Vec<AST> = {
    <mut elements: (<Expr> COMMA)*> <element: Expr?> => {
        match element {
            None => elements,
            Some(e) => { elements.push(e); elements }
        }
    }
}

Conditional: AST = {
    IF <guard: Expr> <then: Block> <els: (ELSE <Block>)?> =>
                    AST::Conditional{guard: Box::new(guard),
                            then_branch: Box::new(then),
                            else_branch: els.map(Box::new)},
}

Return: AST = {
    RETURN <expr: Expr> => AST::Return(Box::new(expr))
}
